### peco

get_os() {
  case "$(uname | awk '{print tolower($0)}')" in
      *'linux'*)  echo 'linux'   ;;
      *'darwin'*) echo 'osx'     ;;
      *'bsd'*)    echo 'bsd'     ;;
      *)          echo 'unknown' ;;
  esac
}

function exists { which $1 &> /dev/null }

function fzf_select_repos() {
  local selected_dir=$(ghq list | fzf-tmux --query "$LBUFFER" --preview "bat --color=always --style=header,grid --line-range :80 $(ghq root)/{}/README.*")

  if [ -n "$selected_dir" ]; then
    BUFFER="cd $(ghq root)/${selected_dir}"
    zle accept-line
  fi

  zle reset-prompt
}
zle -N fzf_select_repos

function fzf_select_git_branch() {
  local selected_branch=$(git for-each-ref --format='%(refname)' --sort=-authordate refs/heads | perl -pne 's{^refs/heads/}{}' | fzf-tmux --query "$LBUFFER" --preview "git show --color=always {}")

  if [ -n "$selected_branch" ]; then
    BUFFER="git checkout ${selected_branch}"
    zle accept-line
  fi

  zle reset-prompt
}
zle -N fzf_select_git_branch

function fzf_ssh_hosts() {
  local selected_host=$(grep "Host " ~/.ssh/config | grep -v '*' | cut -b 6- | fzf-tmux --query "$LBUFFER")

  if [ -n "$selected_host" ]; then
    BUFFER="ssh ${selected_host}"
    zle accept-line
  fi
  zle reset-prompt
}
zle -N fzf_ssh_hosts

function fzf_open_editor() {
  local selected=$(fd | fzf-tmux --query "$LBUFFER" --multi --select-1)

  if [ -n "$selected" ]; then
    ${EDITOR:-vim} "${selected}"
  fi
  zle reset-prompt
}
zle -N fzf_open_editor

bindkey '^]' fzf_select_repos
bindkey '^B' fzf_select_git_branch
bindkey '^\' fzf_ssh_hosts
bindkey '^V' fzf_open_editor

function fkill() {
  local pid=$(ps -ef | sed 1d | fzf -m | awk '{print $2}')

  if [ -n "$pid" ]; then
      echo $pid | xargs kill -9
  fi
}
